key_identifier    = { (ASCII_ALPHANUMERIC | ".")+ }
value             = { (!"," ~ !NEWLINE ~ ANY)* }
list              = { (value ~ ",")+ ~ value }
kvpair            = { key_identifier ~ "=" ~ (list | value) }
struct_identifier = { ASCII_ALPHANUMERIC+ }
struct            = { struct_identifier ~ "." ~ NEWLINE ~ ((kvpair | struct) ~ NEWLINE)* ~ "." }
// manually wrap the struct to avoid the EOI
wrapper           = { ((kvpair | struct) ~ NEWLINE)* ~ (kvpair | struct)? }
file              = { SOI ~ "\u{feff}"? ~ wrapper ~ (NEWLINE | " ")* ~ EOI }

// timetable entries
service_mode      = { ASCII_DIGIT+ }
arrival           = { ASCII_DIGIT{3, 6} }
departure         = { ASCII_DIGIT{3, 6} }
track             = { ASCII_DIGIT+ }
timetable_entry   = {
    (service_mode ~ (";" ~ (arrival ~ "/")? ~ departure?)?) ~ ("$" ~ track)?
}

// event entries
event_param =  { (ASCII_DIGIT | ";")* }
event       =  { event0 }
event0      = _{ event_param ~ ("/" ~ event1)* }
event1      = _{ event_param ~ ("$" ~ event0)* }
